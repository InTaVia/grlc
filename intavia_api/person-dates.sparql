#+ endpoint_in_url: False
#+ pagination: 50
#+ transform: {
#+     "label": "?personLabel",
#+     "uri": "?person",
#+     "date_of_birth": "?begin_of_birth",
#+     "place_of_birth": "?place_of_birth_label",
#+     "$anchor": "uri"
#+   }

PREFIX bds:  <http://www.bigdata.com/rdf/search#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?person ?personLabel ?relevance ?begin_of_birth ?place_of_birth_label
WHERE {
    OPTIONAL {?s ?p ?_birthdateAfter_xsd_date} .
    ?person a crm:E21_Person .
    ?person rdfs:label|skos:prefLabel ?personLabel .
    ?birth crm:P98_brought_into_life ?person .
    ?birth  crm:P4_has_time-span ?date .
    OPTIONAL {
        ?birth crm:P7_took_place_at ?place_of_birth .
        ?place_of_birth crm:P1_is_identified_by ?place_of_birth_label1 .
        ?place_of_birth_label1 rdfs:label ?place_of_birth_label
    }
    ?date crm:P82a_begin_of_the_begin ?begin_of_birth .
    ?personLabel bds:search ?_name_de .
    ?personLabel bds:relevance ?relevance .
    
    FILTER (?begin_of_birth > ?_birthdateAfter_xsd_date)
}
ORDER BY DESC(?person)